# [The syntax to define an optimal control problem](@id manual-abstract-syntax)

The full grammar of [OptimalControl.jl](https://control-toolbox.org/OptimalControl.jl) small *Domain Specific Language* is given below. The idea is to use a syntax that is
- pure Julia (and, as such, effortlessly analysed by the standard Julia parser),
- as close as possible to the mathematical description of an optimal control problem. 

While the syntax will be transparent to those users familiar with Julia expressions (`Expr`'s), we provide examples for every case that should be widely understandable. We rely heavily on [MLStyle.jl](https://thautwarm.github.io/MLStyle.jl) and its pattern matching abilities ğŸ‘ğŸ½ for the semantic pass. Abstract definitions use the macro [`@def`](@ref).

## [Variable](@id manual-abstract-variable)

```julia
:( $v âˆˆ R^$q, variable ) 
:( $v âˆˆ R   , variable ) 
```

A variable (only one is allowed) is a finite dimensional vector or reals that will be *optimised* along with state and control values. To define an (almost empty!) optimal control problem, named `ocp`, having a dimension two variable named `v`, do the following:

```julia
@def begin
    v âˆˆ RÂ², variable
    ...
end
```

!!! caveat
    Note that the full code of the definition above is not provided (hence the `...`) The same is true for most examples below (only those without `...` are indeed complete). Also note that problem definitions must at least include definitions for time, state, control, and dynamics.


Aliases `vâ‚`, `vâ‚‚` (and `v1`, `v2`) are automatically defined and can be used in subsequent expressions instead of `v[1]` and `v[2]`. The user can also define her own aliases for the components (one alias per dimension):

```julia
@def begin
    v = (a, b) âˆˆ RÂ², variable
    ...
end
```

A one dimensional variable can be declared according to

```julia
@def begin
    v âˆˆ R, variable
    ...
end
```

!!! note
    It is also possible to use the following syntax
    ```julia
    @def ocp begin
        v âˆˆ R, variable
        ...
    end
    ```
    that is equivalent to
    ```julia
    ocp = @def begin
        v âˆˆ R, variable
        ...
    end
    ```

## Time

```julia
:( $t âˆˆ [$t0, $tf], time ) 
```

The independent variable or *time* is a scalar bound to a given interval. Its name is arbitrary.

```julia
t0 = 1
tf = 5
@def begin
    t âˆˆ [t0, tf], time
    ...
end
```

One (or even the two bounds) can be variable, typically for minimum time problems (see [Mayer cost](@ref manual-abstract-mayer) section):

```julia
@def begin
    v = (T, Î») âˆˆ RÂ², variable
    t âˆˆ [0, T], time
    ...
end
```

## [State](@id manual-abstract-state)

```julia
:( $x âˆˆ R^$n, state ) 
:( $x âˆˆ R   , state ) 
```

The state declaration defines the name and the dimension of the state:

```julia
@def begin
    x âˆˆ Râ´, state
    ...
end
```

As for the variable, there are automatic aliases (`xâ‚` and `x1` for `x[1]`, *etc.*) and the user can define her own aliases (one per scalar component of the state):

```julia
@def begin
    x = (qâ‚, qâ‚‚, vâ‚, vâ‚‚) âˆˆ Râ´, state
    ...
end
```

## [Control](@id manual-abstract-control)

```julia
:( $u âˆˆ R^$m, control ) 
:( $u âˆˆ R   , control ) 
```

The control declaration defines the name and the dimension of the control:

```julia
@def begin
    u âˆˆ RÂ², control
    ...
end
```

As before, there are automatic aliases (`uâ‚` and `u1` for `u[1]`, *etc.*) and the user can define her own aliases (one per scalar component of the state):

```julia
@def begin
    u = (Î±, Î²) âˆˆ RÂ², control
    ...
end
```

!!! note
    One dimensional variable, state or control are treated as scalars (`Real`), not vectors (`Vector`). In Julia, for `x::Real`, it is possible to write `x[1]` (and `x[1][1]`...) so it is OK (though useless) to write `xâ‚`, `x1` or `x[1]` instead of simply `x` to access the corresponding value. Conversely it is *not* OK to use such an `x` as a vector, for instance as in `...f(x)...` where `f(x::Vector{T}) where {T <: Real}`.

## [Dynamics](@id manual-abstract-dynamics)

```julia
:( âˆ‚($x)($t) == $e1 ) 
```

The dynamics is given in the standard vectorial ODE form:

```math
    \dot{x}(t) = f([t, ]x(t), u(t)[, v])
```

depending on whether it is autonomous / with a variable or not (the parser will detect time and variable dependences,
which entails that time, state and variable must be declared prior to dynamics - an error will be issued otherwise). The symbol `âˆ‚`, or the dotted state name
(`áº‹`), or the keyword `derivative` can be used:

```julia
@def begin
    t âˆˆ [0, 1], time
    x âˆˆ RÂ², state
    u âˆˆ R, control
    âˆ‚(x)(t) == [xâ‚‚(t), u(t)]
    ...
end
```

or

```julia
@def begin
    t âˆˆ [0, 1], time
    x âˆˆ RÂ², state
    u âˆˆ R, control
    áº‹(t) == [xâ‚‚(t), u(t)]
    ...
end
```

or

```julia
@def begin
    t âˆˆ [0, 1], time
    x âˆˆ RÂ², state
    u âˆˆ R, control
    derivative(x)(t) == [xâ‚‚(t), u(t)]
    ...
end
```

Any Julia code can be used, so the following is also OK: 

```julia
ocp = @def begin
    t âˆˆ [0, 1], time
    x âˆˆ RÂ², state
    u âˆˆ R, control
    áº‹(t) == Fâ‚€(x(t)) + u(t) * Fâ‚(x(t))
    ...
end

Fâ‚€(x) = [x[2], 0]
Fâ‚(x) = [0, 1]
```

!!! note
    The vector fields `Fâ‚€` and `Fâ‚` can be defined afterwards, as they only need to be available when the dynamics will be evaluated.

Currently, it is not possible to declare the dynamics component after component, but a simple workaround is to use *aliases* (check the relevant [aliases](@ref manual-abstract-aliases) section below):

```julia
@def damped_integrator begin
    tf âˆˆ R, variable
    t âˆˆ [0, tf], time
    x = (q, v) âˆˆ RÂ², state
    u âˆˆ R, control
    qÌ‡ = v(t)
    vÌ‡ = u(t) - c(t)
    áº‹(t) == [qÌ‡, vÌ‡]
    ...
end
```

## Constraints

```julia
:( $e1 == $e2        ) 
:( $e1 â‰¤  $e2 â‰¤  $e3 ) 
:(        $e2 â‰¤  $e3 ) 
:( $e3 â‰¥  $e2 â‰¥  $e1 ) 
:( $e2 â‰¥  $e1        ) 
```

Admissible constraints can be
- five types: boundary, control, state, mixed, variable,
- linear (ranges) or nonlinear (not ranges),
- equalities or (one or two-sided) inequalities.

Boundary conditions are detected when the expression contains evaluations of the state at initial and / or final time bounds (*e.g.*, `x(0)`), and may not involve the control. Conversely control, state or mixed constraints will involve control, state or both evaluated at the declared time (*e.g.*, `x(t) + u(t)`). 
Other combinations should be detected as incorrect by the parser ğŸ¤ğŸ¾. The variable may be involved in any of the four previous constraints. Constraints involving the variable only are variable constraints, either linear or nonlinear.
In the example below, there are
- two linear boundary constraints,
- one linear variable constraint,
- one linear state constraint,
- one (two-sided) nonlinear control constraint.

```julia
@def begin
    tf âˆˆ R, variable
    t âˆˆ [0, tf], time
    x âˆˆ RÂ², state
    u âˆˆ R, control
    x(0) == [-1, 0]
    x(tf) == [0, 0]
    áº‹(t) == [xâ‚‚(t), u(t)]
    tf â‰¥ 0 
    xâ‚‚(t) â‰¤ 1
    u(t)^2 â‰¤ 1
    ...
end
```

!!! note
    Symbols like `<=` or `>=` are also authorised:

```julia
@def begin
    tf âˆˆ R, variable
    t âˆˆ [0, tf], time
    x âˆˆ RÂ², state
    u âˆˆ R, control
    x(0) == [-1, 0]
    x(tf) == [0, 0]
    áº‹(t) == [xâ‚‚(t), u(t)]
    tf >= 0 
    xâ‚‚(t) <= 1
    u(t)^2 <= 1
    ...
end
```

!!! caveat
    Write either `u(t)^2` or `(u^2)(t)`, not `u^2(t)` since in Julia the latter means `u^(2t)`. Moreover,
    in the case of equalities or of one-sided inequalities, the control and / or the state must belong to the *left-hand side*. The following will error:

```@setup main-repl
using OptimalControl
```

```@repl main-repl
@def begin
    t âˆˆ [0, 2], time
    x âˆˆ RÂ², state
    u âˆˆ R, control
    x(0) == [-1, 0]
    x(2) == [0, 0]
    áº‹(t) == [xâ‚‚(t), u(t)]
    1 â‰¤ xâ‚‚(t)
    -1 â‰¤ u(t) â‰¤ 1
end
```

!!! caveat
     Constraint bounds must be *effective*, that is must not depend on a variable. For instance, instead of
```julia
o = @def begin
    v âˆˆ R, variable
    t âˆˆ [0, 1], time
    x âˆˆ RÂ², state
    u âˆˆ R, control
    -1 â‰¤ v â‰¤ 1
    xâ‚(0) == -1
    xâ‚‚(0) == v # wrong: the bound is not effective (as it depends on the variable)
    x(1) == [0, 0]
    áº‹(t) == [xâ‚‚(t), u(t)]
    âˆ«( 0.5u(t)^2 ) â†’ min
end
```
write
```julia
o = @def begin
    v âˆˆ R, variable
    t âˆˆ [0, 1], time
    x âˆˆ RÂ², state
    u âˆˆ R, control
    -1 â‰¤ v â‰¤ 1
    xâ‚(0) == -1
    xâ‚‚(0) - v == 0 # OK: the boundary contraint may involve the variable
    x(1) == [0, 0]
    áº‹(t) == [xâ‚‚(t), u(t)]
    âˆ«( 0.5u(t)^2 ) â†’ min
end
```

## [Mayer cost](@id manual-abstract-mayer)

```julia                                      
:( $e1 â†’ min ) 
:( $e1 â†’ max ) 
```

Mayer costs are defined in a similar way to boundary conditions and follow the same rules. The symbol `â†’` is used
to denote minimisation or maximisation, the latter being treated by minimising the opposite cost. (The symbol `=>` can also be used.)

```@repl main-repl
@def begin
    tf âˆˆ R, variable
    t âˆˆ [0, tf], time
    x = (q, v) âˆˆ RÂ², state
    u âˆˆ R, control
    tf â‰¥ 0
    -1 â‰¤ u(t) â‰¤ 1
    q(0) == 1
    v(0) == 2
    q(tf) == 0
    v(tf) == 0
    0 â‰¤ q(t) â‰¤ 5
   -2 â‰¤ v(t) â‰¤ 3
    áº‹(t) == [v(t), u(t)]
    tf â†’ min
end
```

## Lagrange cost

```julia
:(       âˆ«($e1) â†’ min ) 
:(     - âˆ«($e1) â†’ min ) 
:( $e1 * âˆ«($e2) â†’ min ) 
:(       âˆ«($e1) â†’ max ) 
:(     - âˆ«($e1) â†’ max ) 
:( $e1 * âˆ«($e2) â†’ max ) 
```

Lagrange (integral) costs are defined used the symbol `âˆ«`, *with parentheses*. The keyword `integral` can also be used:

```julia
@def begin
    t âˆˆ [0, 1], time
    x = (q, v) âˆˆ RÂ², state
    u âˆˆ R, control
    0.5âˆ«(q(t) + u(t)^2) â†’ min
    ...
end
```

or

```julia
@def begin
    t âˆˆ [0, 1], time
    x = (q, v) âˆˆ RÂ², state
    u âˆˆ R, control
    0.5integral(q(t) + u(t)^2) â†’ min
    ...
end
```

The integration range is implicitly equal to the time range, so the cost above is to be understood as
```math
\frac{1}{2} \int_0^1 \left( q(t) + u^2(t) \right) \mathrm{d}t \to \min.
```

As for the dynamics, the parser will detect whether the integrand depends or not on time (autonomous / non-autonomous case).

## Bolza cost

```julia
:( $e1 +       âˆ«($e2)       â†’ min ) 
:( $e1 + $e2 * âˆ«($e3)       â†’ min ) 
:( $e1 -       âˆ«($e2)       â†’ min ) 
:( $e1 - $e2 * âˆ«($e3)       â†’ min ) 
:( $e1 +       âˆ«($e2)       â†’ max ) 
:( $e1 + $e2 * âˆ«($e3)       â†’ max ) 
:( $e1 -       âˆ«($e2)       â†’ max ) 
:( $e1 - $e2 * âˆ«($e3)       â†’ max ) 
:(             âˆ«($e2) + $e1 â†’ min ) 
:(       $e2 * âˆ«($e3) + $e1 â†’ min ) 
:(             âˆ«($e2) - $e1 â†’ min ) 
:(       $e2 * âˆ«($e3) - $e1 â†’ min ) 
:(             âˆ«($e2) + $e1 â†’ max ) 
:(       $e2 * âˆ«($e3) + $e1 â†’ max ) 
:(             âˆ«($e2) - $e1 â†’ max ) 
:(       $e2 * âˆ«($e3) - $e1 â†’ max ) 
```

Quite readily, Mayer and Lagrange costs can be combined into general Bolza costs. For instance as follows:

```julia
@def begin
    p = (t0, tf) âˆˆ RÂ², variable
    t âˆˆ [t0, tf], time
    x = (q, v) âˆˆ RÂ², state
    u âˆˆ RÂ², control
    (tf - t0) + 0.5âˆ«(c(t) * u(t)^2) â†’ min
    ...
end
```

!!! caveat
    The expression must be the sum of two terms (plus, possibly, a scalar factor before the integral), not *more*, so mind the parentheses. For instance, the following errors:

```julia
@def begin
    p = (t0, tf) âˆˆ RÂ², variable
    t âˆˆ [t0, tf], time
    x = (q, v) âˆˆ RÂ², state
    u âˆˆ RÂ², control
    (tf - t0) + q(tf) + 0.5âˆ«( c(t) * u(t)^2 ) â†’ min
    ...
end
```

The correct syntax is
```julia
@def begin
    p = (t0, tf) âˆˆ RÂ², variable
    t âˆˆ [t0, tf], time
    x = (q, v) âˆˆ RÂ², state
    u âˆˆ RÂ², control
    ((tf - t0) + q(tf)) + 0.5âˆ«( c(t) * u(t)^2 ) â†’ min
    ...
end
```

## [Aliases](@id manual-abstract-aliases)

```julia
:( $a = $e1 )
```

The single `=` symbol is used to define not a constraint but an alias, that is a purely syntactic replacement. There are some automatic aliases, *e.g.* `xâ‚` and `x1` for `x[1]` if `x` is the state (same for variable and control, for indices comprised between 1 and 9), and we have also seen that the user can define her own aliases when declaring the [variable](@ref manual-abstract-variable), [state](@ref manual-abstract-state) and [control](@ref manual-abstract-control). Arbitrary aliases can be further defined, as below (compare with previous examples in the [dynamics](@ref manual-abstract-dynamics) section):

```julia
@def begin
    t âˆˆ [0, 1], time
    x âˆˆ RÂ², state
    u âˆˆ R, control
    Fâ‚€ = [xâ‚‚(t), 0]
    Fâ‚ = [0, 1]
    áº‹(t) == Fâ‚€ + u(t) * Fâ‚
    ...
end
```

!!! caveat
    Such aliases do *not* define any additional function and are just replaced textually by the parser. In particular, they cannot be used outside the `@def` `begin ... end` block.

!!! hint
    You can rely on a trace mode for the macro `@def` to look at your code after expansions of the aliases using the `@def ocp ...` syntax and adding `true` after your `begin ... end` block:

```@repl main-repl
@def damped_integrator begin
    tf âˆˆ R, variable
    t âˆˆ [0, tf], time
    x = (q, v) âˆˆ RÂ², state
    u âˆˆ R, control
    qÌ‡ = v(t)
    vÌ‡ = u(t) - c(t)
    áº‹(t) == [qÌ‡, vÌ‡]
end true;
```

!!! caveat
    The dynamics of an OCP is indeed a particular constraint, be careful to use `==` and not a single `=` that would try to define an alias:

```@repl main-repl
double_integrator = @def begin
    tf âˆˆ R, variable
    t âˆˆ [0, tf], time
    x = (q, v) âˆˆ RÂ², state
    u âˆˆ R, control
    qÌ‡ = v
    vÌ‡ = u
    áº‹(t) = [qÌ‡, vÌ‡]
end
```

## Misc

- Declarations (of variable - if any -, time, state and control) must be done first. Then, dynamics, constraints and cost can be introduced in an arbitrary order.
- It is possible to provide numbers / labels (as in math equations) for the constraints to improve readability (this is mostly for future use, typically to retrieve the Lagrange multiplier associated with the discretisation of a given constraint):

```julia
@def damped_integrator begin
    tf âˆˆ R, variable
    t âˆˆ [0, tf], time
    x = (q, v) âˆˆ RÂ², state
    u âˆˆ R, control
    tf â‰¥ 0, (1)
    q(0) == 2, (â™¡)
    qÌ‡ = v(t)
    vÌ‡ = u(t) - c(t)
    áº‹(t) == [qÌ‡, vÌ‡]
    x(t).^2  â‰¤ [1, 2], (state_con) 
    ...
end
```

- Parsing errors should be explicit enough (with line number in the `@def` `begin ... end` block indicated) ğŸ¤ğŸ¾
- Check tutorials and applications in the documentation for further use.

## Known issues

- [Constants and (reverse over forward) AD](https://github.com/control-toolbox/OptimalControl.jl/issues/481)
